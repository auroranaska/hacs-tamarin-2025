
theory KemDH
begin

builtins: diffie-hellman, signing, asymmetric-encryption
functions: h/2

/* This is a restriction that ensures in any rule with the Eq(x,y) action, the terms
matched by the variables x and y are the same. The modeler can then add an Eq action to 
any rule where such an equality is desired, e.g., check that signature verification succeeds.
*/
restriction Eq:
  "All x y #i. Eq(x, y) @ #i ==> x = y"

// Generate Server's long-term (s_sk, s_pk)
rule generate_serverLtk:
  let pkS = pk(~sk) 
  in
  [ Fr(~sk) ]
  -->
  [ !Ltk($S, ~sk), 
    !Pk($S, pkS), 
    Out(pkS) ]

// Client generates DH pair, Kem, and sends public keys
rule Client_Init:
  let x_pk = 'g'^~x_sk
      z_pk = pk(~z_sk)
  in
  [ Fr(~x_sk),
    Fr(~z_sk)]
  --[ ClientInitMessage()]->
  [ Out(<x_pk, z_pk>), StateC($C, ~x_sk, ~z_sk) ]

rule Server_Init:
  let 
    y_pk = 'g'^~y_sk 
    ct = aenc(~ss, z_pk)
    serverSign = sign(<x_pk, y_pk, z_pk, ct>, ~s_sk)
    dh_output = x_pk^~y_sk
    k_s = h(dh_output, ~ss)
    m = <y_pk, serverSign, ct>
  in
  [ !Ltk($S, ~s_sk), 
    Fr(~y_sk),
    Fr(~ss),
    In(<x_pk, z_pk>) ]
  --[ ServerInitMessage(),
      ServerAccepts($S, x_pk, z_pk, y_pk, ct, serverSign, pk(~s_sk), k_s),
      SecretServer($S, k_s) ]->
  [ Out(m) ]

rule Client_Accepts:
  let
    m = <y_pk, serverSign, ct>
    x_pk = 'g'^~x_sk
    z_pk = pk(~z_sk)
    
    ss = adec(ct, ~z_sk)
    dh_output = y_pk^~x_sk
    k_c = h(dh_output, ss)
  in
  [ StateC($C, ~x_sk, ~z_sk),
    !Pk($S, s_pk),
    In(m) ]
  --[  Eq(verify(serverSign, <x_pk, y_pk, z_pk, ct>, s_pk), true),
      ClientAccepts($S, x_pk, z_pk, y_pk, ct, serverSign, s_pk, k_c),
      SecretClient($S, k_c),
      ClientRecvMessage() ]->
  [ ]

//Attacker learns the server's long-term key
rule Reveal_LTK:
  [ !Ltk($S, ~sk)]
  --[RevealLtk($S, ~sk)]->
  [ Out(~sk) ]

// Sanity Traces to check executability of the model
lemma ExecutabilityClient:
  exists-trace
  "Ex #i. ClientInitMessage() @ #i"

lemma ExecutabilityServer:
  exists-trace
  "Ex #i. ServerInitMessage() @ #i"

lemma ExecutabilityClientEnd:
  exists-trace
  "Ex #i. ClientRecvMessage() @ #i"

lemma BothPartiesCanReachEnd:
  exists-trace
  "Ex S x_pk z_pk y_pk ct sig s_pk key #i #j. i < j &
     ServerAccepts(S, x_pk, z_pk, y_pk, ct, sig, s_pk, key) @ #i &
   ClientAccepts(S, x_pk, z_pk, y_pk, ct, sig, s_pk, key) @ #j "

// Secrecy of established session key
// Does not hold. (The client could have been an attacker, therefore it knows the established key.)
lemma ServerKeySecrecy:
  "All S key #i. SecretServer(S, key) @ #i 
  ==> (not Ex #j. K(key) @ #j)
    | (Ex s_sk #j. RevealLtk(S, s_sk)@ #j)"

// Verifies True.
lemma ClientKeySecrecy:
  "All S key #i. SecretClient(S, key) @ #i 
  ==> (not Ex #j. K(key) @ #j)
    | (Ex s_sk #j. RevealLtk(S, s_sk)@ #j)"

// Server Authentication at the end of the protocol
lemma ServerAuthentication:
  "All S x_pk z_pk y_pk ct sig s_pk key #i. 
   ClientAccepts(S, x_pk, z_pk, y_pk, ct, sig, s_pk, key) @ #i 
   ==>  
      (Ex #j. j<i & ServerAccepts(S, x_pk, z_pk, y_pk, ct, sig, s_pk, key) @ #j)
    | (Ex s_sk #j. RevealLtk(S, s_sk)@ #j)"

end
