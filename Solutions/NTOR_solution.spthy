theory NTOR
begin

builtins: diffie-hellman
functions: hash1/5, hash2/2

/* This is a restriction that ensures in any rule with the Eq(x,y) action, the terms
matched by the variables x and y are the same. The modeler can then add an Eq action to 
any rule where such an equality is desired, e.g., check that signature verification succeeds.
*/
restriction Eq:
  "All x y #i. Eq(x, y) @ #i ==> x = y"

// Generate Server's long-term (s_sk, s_pk)
rule generate_serverLtk:
  let pkS = 'g'^~sk 
  in
  [ Fr(~sk) ]
  -->
  [ !Ltk($S, ~sk), 
    !Pk($S, pkS), 
    Out(pkS) ]

// Client generates DH pair, and sends public key
rule Client_Init:
  let x_pk = 'g'^~x_sk
  in
  [ Fr(~x_sk)]
  --[ ClientInitMessage()]->
  [ Out(<x_pk>), StateC($C, ~x_sk) ]

rule Server_Init:
  let 
    y_pk = 'g'^~y_sk
    s_pk = 'g'^~s_sk
    dh_output1 = x_pk^~y_sk
    dh_output2 = x_pk^~s_sk
    mk_s = hash1(x_pk, y_pk, s_pk, dh_output1, dh_output2)
    auth = hash2(mk_s, '0')
    k_s = hash2(mk_s, '1')
    m = <y_pk, auth>
  in
  [ !Ltk($S, ~s_sk), 
    Fr(~y_sk)
  , In(x_pk) ]
  --[ ServerInitMessage(),
      ServerAccepts($S, x_pk, y_pk, s_pk, auth, k_s),
      SecretServer($S, k_s) ]->
  [ Out(m) ]

rule Client_Accepts:
  let
    m = <y_pk, auth>
    x_pk = 'g'^~x_sk
    dh_output1 = y_pk^~x_sk
    dh_output2 = s_pk^~x_sk
    mk_c = hash1(x_pk, y_pk, s_pk, dh_output1, dh_output2)
    auth_c = hash2(mk_c, '0')
    k_c = hash2(mk_c, '1')
  in
  [ StateC($C, ~x_sk),
    !Pk($S, s_pk),
    In(m) ]
  --[  Eq(auth, auth_c),
      ClientAccepts($S, x_pk, y_pk, s_pk, auth, k_c),
      SecretClient($S, k_c),
      ClientRecvMessage()]->
  [ ]

//Attacker learns the server's long-term key
rule Reveal_LTK:
  [ !Ltk($S, ~sk)]
  --[RevealLtk($S, ~sk)]->
  [ Out(~sk) ]

// Sanity Traces to check executability of the model
lemma ExecutabilityClient:
  exists-trace
  "Ex #i. ClientInitMessage() @ #i"

lemma ExecutabilityServer:
  exists-trace
  "Ex #i. ServerInitMessage() @ #i"

lemma ExecutabilityClientEnd:
  exists-trace
  "Ex #i. ClientRecvMessage() @ #i"

lemma BothPartiesCanReachEnd:
  exists-trace
  "Ex S x_pk y_pk s_pk auth key #i #j. i < j &
     ServerAccepts(S, x_pk, y_pk, s_pk, auth, key) @ #i &
   ClientAccepts(S, x_pk, y_pk, s_pk, auth, key) @ #j "


// Secrecy of established session key
// Does not hold. (The client could have been an attacker, therefore it knows the established key.)
lemma ServerKeySecrecy:
  "All S key #i. SecretServer(S, key) @ #i 
  ==> (not Ex #j. K(key) @ #j)
    | (Ex s_sk #j. RevealLtk(S, s_sk)@ #j)"

// Verifies True.
lemma ClientKeySecrecy:
  "All S key #i. SecretClient(S, key) @ #i 
  ==> (not Ex #j. K(key) @ #j)
    | (Ex s_sk #j. RevealLtk(S, s_sk)@ #j)"

// Server Authentication at the end of the protocol
lemma ServerAuthentication:
  "All S x_pk y_pk s_pk auth key #i. 
   ClientAccepts(S, x_pk, y_pk, s_pk, auth, key) @ #i 
   ==>  
      (Ex #j. j<i & ServerAccepts(S, x_pk, y_pk, s_pk, auth, key) @ #j)
    | (Ex s_sk #j. RevealLtk(S, s_sk)@ #j)"

end
